# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprland-oceania
provides=('hyprland')
conflicts=('hyprland')
pkgver=0.32.0
pkgrel=1
pkgdesc='a highly customizable dynamic tiling Wayland compositor, custom PKGBUILD to ensure users get the most up to date version'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/hyprland"
license=(BSD)
depends=(cairo
         gcc-libs
         glibc
         glslang
         libdisplay-info
         libdrm
         libglvnd
         libinput
         libliftoff
         libx11
         libxcb
         libxcomposite
         libxfixes
         libxkbcommon
         libxrender
         opengl-driver
         pango
         pixman
         pixman
         polkit
         seatd
         systemd-libs
         vulkan-icd-loader
         vulkan-validation-layers
         wayland
         wayland-protocols
         xcb-proto
         xcb-util
         xcb-util-errors
         xcb-util-keysyms
         xcb-util-renderutil
         xcb-util-wm
         xorg-xinput
         xorg-xwayland)
depends+=(libdisplay-info.so)
makedepends=(cmake
             gdb
             meson
             ninja
             vulkan-headers
             xorgproto)
_archive="hyprland-$pkgver"
source=("$_archive.tar.gz::$url/releases/download/v$pkgver/source-v$pkgver.tar.gz")
sha256sums=('SKIP')

prepare() {
	ln -sf hyprland-source "$_archive"
	cd "$_archive"

	sed -i -e '/^release:/{n;s/-D/-DCMAKE_SKIP_RPATH=ON -D/}' Makefile
}

build() {
	cd hyprland-source
	meson setup build \
    --prefix     /usr \
    --libexecdir lib \
    --sbindir    bin \
    --buildtype  release \
    --wrap-mode  nodownload \
    -D           b_lto=true \
    -D           b_pie=true \
    -D           default_library=shared \
    -D           xwayland=enabled

  ln -sf wlroots build/subprojects/wlroots/include/wlr
  meson compile -C build
}

package() {
	cd hyprland-source

  meson install -C build \
    --destdir "$pkgdir" \
    --skip-subprojects hyprland-protocols

  rm -rf "$pkgdir/usr/include/hyprland/wlroots/wlr"

  # resolve conflicts with system wlr
  rm -f "$pkgdir/usr/lib/libwlroots.so"
  rm -rf "$pkgdir/usr/lib/pkgconfig"
  rm -rf "$pkgdir/usr/share/xdg-desktop-portal"
  rm -rf "$pkgdir/usr/include/wlr"

  # license
  install -Dm0644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
